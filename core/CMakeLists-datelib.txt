set(DateWrapper_PREFIX "${CMAKE_CURRENT_BINARY_DIR}/datelib_wrapper")

ExternalProject_Add(
  DateWrapperExternal

  GIT_REPOSITORY "https://github.com/ravenvz/date_wrapper"
  GIT_TAG "v0.1.2"

  PREFIX "${DateWrapper_PREFIX}"
  UPDATE_COMMAND ""
  PATCH_COMMAND ""
  INSTALL_COMMAND ""
  CMAKE_ARGS -DCMAKE_WINDOWS_EXPORT_ALL_SYMBOLS=True -DBUILD_SHARED_LIBS=True -DCMAKE_BUILD_TYPE=Release -DCMAKE_RUNTIME_OUTPUT_DIRECTORY=${CMAKE_RUNTIME_OUTPUT_DIRECTORY}

  SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/${THIRD_PARTY_LIBS_FOLDER_PREFIX}/date_wrapper"
)

set(LIBPREFIX "${CMAKE_SHARED_LIBRARY_PREFIX}")
set(LIBSUFFIX "${CMAKE_SHARED_LIBRARY_SUFFIX}")

ExternalProject_Get_Property(DateWrapperExternal source_dir binary_dir)

set(DateWrapper_INCLUDE_DIRS "${source_dir}/src/date_wrapper/include")
set(DateWrapper_LOCATION "${binary_dir}")

if (WIN32)
	set(DateWrapper_LIBRARY "${source_dir}/lib/Release/${LIBPREFIX}date_wrapper.lib")
else()
	set(DateWrapper_LIBRARY "${source_dir}/lib/${LIBPREFIX}date_wrapper${LIBSUFFIX}")
endif()

# Workaround against Cmake not downloading lib before using it
file(MAKE_DIRECTORY ${DateWrapper_INCLUDE_DIRS})

# TODO find more elegant way to handle this
set(date_INCLUDE_DIRS "${source_dir}/src/date_wrapper/third_party/date")

add_library(DateWrapper UNKNOWN IMPORTED)
add_dependencies(DateWrapper DateWrapperExternal)

set_target_properties(DateWrapper PROPERTIES
    IMPORTED_LOCATION "${DateWrapper_LIBRARY}"
    INTERFACE_INCLUDE_DIRECTORIES "${DateWrapper_INCLUDE_DIRS}"
)
